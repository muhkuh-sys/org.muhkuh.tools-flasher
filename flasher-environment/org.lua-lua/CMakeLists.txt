cmake_minimum_required(VERSION 3.7)

SET(LUA_VERSION "5.4.6")

#----------------------------------------------------------------------------
#
# Extract the source and build the LUA interpreter.
#

SET(LUA_CMAKE_ARGS "")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}")
LIST(APPEND LUA_CMAKE_ARGS "-DLUA_ICON=${CMAKE_CURRENT_SOURCE_DIR}/icon/lua.ico")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_DLL_TEST=${CMAKE_HOME_DIRECTORY}/cmake/tests/mingw_dll_dependencies.py")
LIST(APPEND LUA_CMAKE_ARGS "-DVERSION=${LUA_VERSION}")
LIST(APPEND LUA_CMAKE_ARGS "-DPYTHON_INTERPRETER=${PYTHON_INTERPRETER}")
LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_INSTALL_PREFIX=${CMAKE_INSTALL_PREFIX}")

IF(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
	LIST(APPEND LUA_CMAKE_ARGS "-DCMAKE_RC_COMPILER=${CMAKE_RC_COMPILER}")
ENDIF(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")

ExternalProject_Add(TARGET_lua54
                    PREFIX ${CMAKE_CURRENT_BINARY_DIR}/lua
                    URL ${CMAKE_CURRENT_SOURCE_DIR}/lua-${LUA_VERSION}.tar.gz
                    URL_HASH SHA1=83f41abf92620dd15f022e6f863807b07e318495
                    PATCH_COMMAND "${PYTHON_INTERPRETER}" ${CMAKE_HOME_DIRECTORY}/cmake/tools/apply_patches.py --working-folder ${CMAKE_CURRENT_BINARY_DIR}/lua/src/TARGET_lua54 --copy-folder ${CMAKE_CURRENT_SOURCE_DIR}/templates --patch-folder ${CMAKE_CURRENT_SOURCE_DIR}/patches --strip 1
                    CMAKE_ARGS ${LUA_CMAKE_ARGS}
                    TEST_COMMAND ${CMAKE_MAKE_PROGRAM} test
                    INSTALL_COMMAND ${CMAKE_MAKE_PROGRAM} install
)

SET(SRC_FOLDER "${CMAKE_CURRENT_BINARY_DIR}/lua/src/TARGET_lua54")
SET(BLD_FOLDER "${CMAKE_CURRENT_BINARY_DIR}/lua/src/TARGET_lua54-build")

INSTALL(PROGRAMS ${BLD_FOLDER}/lua5.4${CMAKE_EXECUTABLE_SUFFIX}
        DESTINATION ${FLASHER_PACKAGE_DIR})
INSTALL(FILES ${BLD_FOLDER}/lua5.4${CMAKE_SHARED_LIBRARY_SUFFIX}
        DESTINATION ${FLASHER_PACKAGE_DIR})

SET(LUA_LIBRARIES    "${CMAKE_CURRENT_BINARY_DIR}/lua/src/TARGET_lua54-build/lua5.4${CMAKE_SHARED_LIBRARY_SUFFIX}"  CACHE INTERNAL "The complete path to the lua5.4 library.")
SET(LUA_INCLUDE_DIR  "${CMAKE_BINARY_DIR}/install/dev/include/lua5.4"  CACHE INTERNAL "The include folder for the lua5.4 library.")
