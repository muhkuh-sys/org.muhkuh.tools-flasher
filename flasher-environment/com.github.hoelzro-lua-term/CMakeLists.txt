cmake_minimum_required(VERSION 3.2.3)

PROJECT("lua-term")

SET(VERSION_EXTERNAL "0.7")
SET(VERSION_PACKAGE  "5")
SET(PROJECT_VERSION  "${VERSION_EXTERNAL}.${VERSION_PACKAGE}")


#----------------------------------------------------------------------------
#
# Build the project.
#

SET(SUB_CMAKE_ARGS "")
LIST(APPEND SUB_CMAKE_ARGS "-DCMAKE_C_FLAGS=${CMAKE_C_FLAGS}")
LIST(APPEND SUB_CMAKE_ARGS "-DCMAKE_SYSTEM_NAME=${CMAKE_SYSTEM_NAME}")
LIST(APPEND SUB_CMAKE_ARGS "-DCMAKE_C_COMPILER=${CMAKE_C_COMPILER}")
LIST(APPEND SUB_CMAKE_ARGS "-DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}")
IF(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
	LIST(APPEND SUB_CMAKE_ARGS "-DCMAKE_RC_COMPILER=${CMAKE_RC_COMPILER}")
ENDIF(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
LIST(APPEND SUB_CMAKE_ARGS "-DLUA_LIBRARIES=${LUA_LIBRARIES}")
LIST(APPEND SUB_CMAKE_ARGS "-DLUA_INCLUDE_DIR=${LUA_INCLUDE_DIR}")
LIST(APPEND SUB_CMAKE_ARGS "-DPYTHON_INTERPRETER=${PYTHON_INTERPRETER}")
LIST(APPEND SUB_CMAKE_ARGS "-DTEST_SCRIPT_FOLDER=${CMAKE_HOME_DIRECTORY}/cmake/tests")

ExternalProject_Add(TARGET_lua-term
                    PREFIX ${CMAKE_CURRENT_BINARY_DIR}/lua-term
                    URL ${CMAKE_CURRENT_SOURCE_DIR}/lua-term-0.07.tar.gz
                    URL_HASH SHA1=c0e8fc9054e784fb10af9dca015f713442402833
                    DEPENDS TARGET_lua54
                    PATCH_COMMAND ${CMAKE_COMMAND} -E copy_directory ${CMAKE_CURRENT_SOURCE_DIR}/copy-over ${CMAKE_CURRENT_BINARY_DIR}/lua-term/src/TARGET_lua-term
                    CMAKE_ARGS ${SUB_CMAKE_ARGS}
                    TEST_COMMAND make test
                    INSTALL_COMMAND true
)

SET(SRC_FOLDER "${CMAKE_CURRENT_BINARY_DIR}/lua-term/src/TARGET_lua-term")
SET(BLD_FOLDER "${CMAKE_CURRENT_BINARY_DIR}/lua-term/src/TARGET_lua-term-build")

# Install the lua module.
INSTALL(FILES ${BLD_FOLDER}/core${CMAKE_SHARED_LIBRARY_SUFFIX}
        DESTINATION ${FLASHER_PACKAGE_DIR}/lua_plugins/term)

INSTALL(FILES ${SRC_FOLDER}/term/colors.lua ${SRC_FOLDER}/term/cursor.lua ${SRC_FOLDER}/term/init.lua
        DESTINATION ${FLASHER_PACKAGE_DIR}/lua/term)
